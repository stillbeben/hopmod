stats_db_schema_filename = "scripts/stats/stats.db.schema"
pending_sql_filename = "scripts/stats/data/pending_sql"

absolute_stats_db_filename = (concatword $PWD [/] $stats_db_filename)
createdb = (! (path? $stats_db_filename) )
latestgame = (now)
upload_player_stats = 0
recording_stats = 0

new sqlite3 statsdb
statsdb onerror [log_error [SQL error in statsdb eval: @arg1]]

statsdb onbusy [
    parameters sql
    logfile $pending_sql_filename pending_sql
    pending_sql $sql
    pending_sql $bindings
    close_logfile pending_sql
]

statsdb open $stats_db_filename
statsdb busy_timeout 50

if $createdb [
    statsdb eval [begin transaction]
    statsdb eval (readfile $stats_db_schema_filename)
    statsdb eval [commit transaction]
]

event_handler $onconnect [
    parameters cn
    if $recording_stats [
        if (! (player_has_var $cn stats_id)) [
            statsdb eval [begin transaction] []
            create_player_record $cn
            statsdb eval [commit transaction] []
        ]
    ] [create_match_record]
]

event_handler $ondisconnect [
    parameters cn reason
    if $recording_stats [
        statsdb eval [begin transaction] []
        update_player_record $cn
        statsdb eval [commit transaction] []
        
        discmsg = "disconnected"
        if (! (= $reason $DISC_NONE)) [discmsg = (format "disconnected (%1)" (disc_reason $reason))]
    ]
]

event_handler $ondamage [
    parameters target actor damage gun
    player_var $actor damage (+ $damage (player_var $actor damage))
]

event_handler $ondeath [
    parameters offender victim
    
    if (= $offender $victim) [
        player_var $offender suicides (+ 1 (player_var $offender suicides))
    ] [
        player_var $offender frags (+ 1 (player_var $offender frags))
        player_var $offender (player_gun $offender) (+ 1 (player_var $offender (player_gun $offender)))
        
        local teamkill (&& (teamgame) [strcmp (player_team $offender) (player_team $victim)])
        
        if (&& (! $teamkill) (= 1 (player_var $victim had_flag))) [
            player_var $offender ctf_defended (+ 1 (player_var $offender ctf_defended))
        ]
    ]
    
    player_var $victim deaths (+ 1 (player_var $victim deaths))
    player_var $victim had_flag 0
]

event_handler $onitempickup [
    parameters cn type
    player_var $cn $type (+ 1 (player_var $cn $type))
]


event_handler $onendgame stats_endgame
event_handler $onintermission stats_endgame

stats_endgame = [
    if $recording_stats [
        update_player_records
        
        local duration (- (div $gametime 1000) (secsleft))
        statsdb eval [update matches set duration = $duration where id = $match_id] []
        
        local playerscount 0
        statsdb eval [select count(*) as playerscount from players where match_id = $match_id] [playerscount = (column playerscount)]
        statsdb eval [update matches set players = $playerscount where id = $match_id] []
        
        if (capturep) [
            foreach (teams) [
                local team_id (get_captureteam_id $arg1)
                local score (capture_teamscore $arg1)
                statsdb eval [update captureteams set score=$score where id=$team_id] []
            ]
        ]
        
        if (> $stats_filter_policy_min_frags 0) [
            local sumfrags 0
            statsdb eval [select sum(frags) from players where match_id = $match_id] [sumfrags = (column "sum(frags)")]
            if (< $sumfrags $stats_filter_policy_min_frags) [
                delete_match_stats $match_id
                match_id = -1
                log_status [Discarding stats for last match for not meeting minimum frags requirement of @stats_filter_policy_min_frags frags.]
            ]
        ]
        
        if (&& (< $duration $stats_filter_policy_min_duration) (!= $match_id -1)) [
            delete_match_stats $match_id
            match_id = -1
            log_status [Discarding stats for last match for not meeting minimum duration requirement of @stats_filter_policy_min_duration seconds.]
        ]
    ]
    
    recording_stats = 0
]

event_handler $onmapchanged [
    latestgame = (now)
    create_match_record
]

event_handler $onrename [
    parameters cn oldname newname
    if $recording_stats [
        update_player_record $cn
        create_player_record $cn
    ]
]

event_handler $onreteam [
    parameters cn oldteam newteam
    if $recording_stats [
        update_player_record $cn
        create_player_record $cn
    ]
]

event_handler $ontakeflag [
    parameters cn
    player_var $cn ctf_pickups (+ 1 (player_var $cn ctf_pickups))
]

event_handler $ondropflag [
    parameters cn
    player_var $cn ctf_drops (+ 1 (player_var $cn ctf_drops))
    player_var $cn had_flag 1
]

event_handler $onreturnflag [
    parameters cn
    player_var $cn ctf_returns (+ 1 (player_var $cn ctf_returns))
]

event_handler $onresetflag [
    parameters owner
    local team_id (player_var $owner ctf_team_id)
    statsdb eval [update ctfteams set resets=resets+1 where id=$team_id] []
]

event_handler $onscoreflag [
    parameters cn
    player_var $cn ctf_scored (+ 1 (player_var $cn ctf_scored))
]

event_handler $oncapturebase [
    parameters team
    local team_id (get_captureteam_id $team)
    statsdb eval [update captureteams set captured=captured+1 where id=$team_id] []
]

event_handler $onlostbase [
    parameters team
    local team_id (get_captureteam_id $team)
    statsdb eval [update captureteams set lost=lost+1 where id=$team_id] []
]

event_handler $onwincapture [
    parameters team
    local team_id (get_captureteam_id $team)
    statsdb eval [update captureteams set win=1 where id=$team_id] []
]

event_handler $onshutdown [
    if $recording_stats update_player_records
    statsdb close
]

create_match_record = [
    recording_stats = (newstatsp)
    
    local input_datetime (now)
    
    if $recording_stats [
        statsdb eval [begin transaction] []
        statsdb eval [insert into matches (datetime,gamemode,mapname) values 
            ($input_datetime, $gamemode, $mapname)] []
        match_id = (statsdb last_rowid)
        if (ctfp) create_ctfteam_records
        if (capturep) create_captureteam_records
        foreach (players) [create_player_record $arg1]
        statsdb eval [commit transaction] []
    ] [
        match_id = -1
        foreach (players) [clearplayerstatvars $arg1]
    ]
]

clearplayerstatvars = [
    player_var $arg1 frags 0
    player_var $arg1 deaths 0
    player_var $arg1 suicides 0
    
    player_var $arg1 ctf_pickups 0
    player_var $arg1 ctf_drops 0
    player_var $arg1 ctf_scored 0
    player_var $arg1 ctf_defended 0
    player_var $arg1 ctf_returns 0
    
    player_var $arg1 pistol 0
    player_var $arg1 rifle 0
    player_var $arg1 shotgun 0
    player_var $arg1 chaingun 0
    player_var $arg1 rocketlauncher 0
    player_var $arg1 grenadelauncher 0
    
    player_var $arg1 boost 0
    player_var $arg1 health 0
    player_var $arg1 shells 0
    player_var $arg1 bullets 0
    player_var $arg1 rockets 0
    player_var $arg1 rounds 0
    player_var $arg1 grenades 0
    player_var $arg1 cartridges 0
    player_var $arg1 greenarmor 0
    player_var $arg1 yellowarmor 0
    player_var $arg1 quad 0
]

create_player_record = [
    parameters cn
    
    clearplayerstatvars $cn
    
    if $recording_stats [
        local name (player_name $cn)
        local team (player_team $cn)
        local ipaddr (player_ip $cn)
        
        statsdb eval [insert into players (match_id,name,team,ipaddr) values 
            ($match_id, $name, $team, $ipaddr)] []
        
        player_var $cn stats_id (statsdb last_rowid)
        
        if (itemsgame) [create_items_record $cn]
        if (mongunsp) [create_gunusage_record $cn]
        if (ctfp) [create_ctfplayer_record $cn]
        if (capturep) [create_captureplayer_record $cn]
    ]
]

create_items_record = [
    parameters cn
    local player_id (player_var $cn stats_id)
    statsdb eval [insert into itempickups (player_id) values ($player_id)] []
    player_var $cn items_id (statsdb last_rowid)
]

create_gunusage_record = [
    parameters cn
    local player_id (player_var $cn stats_id)
    statsdb eval [insert into gunusage (player_id) values ($player_id)] []
    player_var $cn gunusage_id (statsdb last_rowid)
]

update_player_record = [
    parameters cn
    if (player_has_var $cn stats_id) [
    
        local id (player_var $cn stats_id)
        local name (player_name $cn)
        local team (player_team $cn)
        local frags (player_var $cn frags)
        local deaths (player_var $cn deaths)
        local suicides (player_var $cn suicides)
        local hits (player_hits $cn)
        local misses (player_misses $cn)
        local damage (player_var $cn damage)
        local kicked (player_var $cn kicked)
        
        if (strcmp $kicked "") [kicked = 0]
        
        statsdb eval [update players set name=$name,team=$team,frags=$frags,deaths=$deaths,
            suicides=$suicides,hits=$hits,misses=$misses,damage=$damage,kicked=$kicked where id=$id] []
        
        if (player_has_var $cn items_id) [update_items_record $cn]
        if (player_has_var $cn gunusage_id) [update_gunusage_record $cn]
        if (player_has_var $cn ctf_id) [update_ctfplayer_record $cn]
        if (player_has_var $cn capture_id) [update_captureplayer_record $cn]
    ]
]

update_items_record = [
    parameters cn
    
    local items_id (player_var $cn items_id)
    local boost (player_var $cn boost)
    local health (player_var $cn health)
    local shells (player_var $cn shells)
    local bullets (player_var $cn bullets)
    local rockets (player_var $cn rockets)
    local rounds (player_var $cn rounds)
    local grenades (player_var $cn grenades)
    local cartridges (player_var $cn cartridges)
    local greenarmor (player_var $cn greenarmor)
    local yellowarmor (player_var $cn yellowarmor)
    local quad (player_var $cn quad)
    
    statsdb eval [update itempickups set boost=$boost,health=$health,shells=$shells,bullets=$bullets,
        rockets=$rockets,rounds=$rounds,grenades=$grenades,cartridges=$cartridges,greenarmor=$greenarmor,
        yellowarmor=$yellowarmor,quad=$quad where id=$items_id] []
]

update_gunusage_record = [
    parameters cn
    
    local gunusage_id (player_var $cn gunusage_id)
    local pistol (player_var $cn pistol)
    local rifle (player_var $cn pistol)
    local shotgun (player_var $cn shotgun)
    local chaingun (player_var $cn chaingun)
    local rocketlauncher (player_var $cn rocketlauncher)
    local grenadelauncher (player_var $cn grenadelauncher)
    
    statsdb eval [update gunusage set pistol=$pistol,rifle=$rifle,shotgun=$shotgun,chaingun=$chaingun,
        rocketlauncher=$rocketlauncher,grenadelauncher=$grenadelauncher where id=$gunusage_id] []
]

update_player_records = [
    statsdb eval [begin transaction] []
    foreach (players) [update_player_record $arg1]
    statsdb eval [commit transaction] []
]

create_ctfteam_records = [
    statsdb eval [begin transaction] []
    foreach (teams) [create_ctfteam_record $arg1]
    statsdb eval [commit transaction] []
]

create_ctfteam_record = [
    reference teamname arg1
    statsdb eval [insert into ctfteams (match_id,name) values ($match_id,$teamname)] []
    result (statsdb last_rowid)
]

get_ctfteam_id = [
    parameters teamname
    local id -1
    statsdb eval [select id from ctfteams where name=$teamname and match_id=$match_id] [id = (column id)]
    if (= $id -1) [id = (create_ctfteam_record $teamname)]
    result $id
]

create_ctfplayer_record = [
    parameters cn
    
    //TODO resume existing record
    
    local player_id (player_var $cn stats_id)
    local team_id (get_ctfteam_id (player_team $cn))
    
    statsdb eval [insert into ctfplayers (player_id,team_id) values ($player_id,$team_id)] []
    
    player_var $cn ctf_id (statsdb last_rowid)
    player_var $cn ctf_team_id $team_id
]

update_ctfplayer_record = [
    parameters cn
    
    local ctfplayer_id (player_var $cn ctf_id)
    local pickups (player_var $cn ctf_pickups)
    local drops (player_var $cn ctf_drops)
    local scored (player_var $cn ctf_scored)
    local defended (player_var $cn ctf_defended)
    local returns (player_var $cn ctf_returns)
    local teamkills (player_var $cn teamkills)
    
    if (strcmp $teamkills "") [teamkills = 0]
    
    statsdb eval [update ctfplayers set pickups=$pickups,drops=$drops,scored=$scored,
        defended=$defended,returns=$returns,teamkills=$teamkills where id=$ctfplayer_id] []
    
    local team_id (player_var $cn ctf_team_id)
    statsdb eval [update ctfteams set score=score+$scored,drops=drops+$drops,
        returns=returns+$returns where id=$team_id] []
]

create_captureteam_record = [
    parameters teamname
    statsdb eval [insert into captureteams (match_id,name) values ($match_id,$teamname)] []
    result (statsdb last_rowid)
]

create_captureteam_records = [
    statsdb eval [begin transaction] []
    foreach (teams) [
        create_captureteam_record $arg1
    ]
    statsdb eval [commit transaction] []
]

get_captureteam_id = [
    parameters teamname
    local id -1
    statsdb eval [select id from captureteams where name=$teamname and match_id=$match_id] [id = (column id)]
    if (= $id -1) [id = (create_captureteam_record $teamname)]
    result $id
]

create_captureplayer_record = [
    parameters cn
    
    local player_id (player_var $cn stats_id)
    local team_id (get_captureteam_id (player_team $cn))
    
    statsdb eval [insert into captureplayers (player_id,team_id) values ($player_id,$team_id)] []
    
    player_var $cn capture_id (statsdb last_rowid)
]

update_captureplayer_record = [
    parameters cn
    local teamkills (player_var $cn teamkills)
    if (strcmp $teamkills "") [teamkills = 0]
    statsdb eval [update captureplayers set teamkills=$teamkills] []
]

delete_match_stats = [
    parameters mid
    
    statsdb eval [delete from matches where id = $mid] []
    statsdb eval [delete from ctfteams where match_id = $mid] []
    statsdb eval [delete from captureteams where match_id = $mid] []

    statsdb eval [select * from players where match_id = $mid] [
        local pid (column id)
        
        eval [delete from itempickups where player_id = $pid] []
        eval [delete from gunusage where player_id = $pid] []
        eval [delete from ctfplayers where player_id = $pid] []
        eval [delete from captureplayers where player_id = $pid] []
    ]
    
    statsdb eval [delete from players where match_id = $mid] []
]

showaliases = [
    parameters target
    local exclude (player_name $target)
    if (! $recording_stats) [throw runtime.function.showaliases.no_data]
    local ipaddr (player_ip $target)
    local namelist ""
    statsdb eval [select distinct name from players where ipaddr=$ipaddr and name<>$exclude] [
        namelist = (concatword $namelist [@(column name), ])
    ]
    result $namelist
]

showaliasesbyip = [
    parameters ip
    local namelist ""
    statsdb eval [select distinct name from players where ipaddr=$ip] [
        namelist = (concatword $namelist [@(column name), ])
    ]
    result $namelist
]

exec "scripts/stats/statscmd.cs"
