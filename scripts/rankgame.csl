
if (! (&& (&& (symbol? ranked_gametime) (symbol? ranked_map)) (symbol? ranked_mode))) [
    throw runtime.svconf.rankgame.missing_arguments
]

if (! $record_player_stats) [
    msg (err "Rank game setup failed! Player stats are disabled.")
    throw runtime.svconf.rankgame.stats_disabled
]

if (! $allow_mm_locked) [
    msg (err "Rank game setup failed! Server doesn't allow locked mastermode.")
    throw runtime.svconf.rankgame.mm_locked_disabled
]

print_details = [
    result (format "playing %1 on map %2 for 10 minutes." $ranked_mode $ranked_map)
]

if $playercount [
    ranked_gametime = (max $ranked_gametime 10)
    
    if (> $ranked_gametime 10) [
        announce (format "The next game will be ranked and will begin in %1. Details: %2" (fduration $ranked_gametime) (print_details))
    ]
    
    sleep (* (- $ranked_gametime 10) 1000) [
        announce (format "Get ready for next rank game! Details: %1" (print_details))
        recorddemo 1 (format "stats/data/%1.dmo" (gameid))
        
        sleep 10000 [ //start ranked game
            mastermode $MM_LOCKED
            changemap $ranked_mode $ranked_map
            event_handler $onmapchanged [
                endrankgame
                cancel_handler
            ]
            foreach (players) [
                reference cn arg1
                player_var $cn rankname (player_name $cn)
            ]
            rankgame = 1
        ]
    ]
] [log "Rank game configuration failed: no players are connected."]

named_event_handler $onconnect [
    reference cn arg1
    privmsg $cn (info "A rank game is in progress.")
    
    if (&& (player_var $cn rankname) (strcmp (player_name $cn) (player_var $cn rankname))) [
        unspec $cn
    ]
] "rankgame"

named_event_handler $onrename [
    reference cn arg1
    reference oldname arg2
    reference newname arg3
    
    local allowed (strcmp $newname (player_var $cn rankname))
    
    if (! (strcmp (player_status $cn) "spectator")) [
        if (! $allowed) [
            spec $cn
            privmsg $cn (info (format "You cannot change your name during a rank game, rename back to %1 to rejoin the game." (player_var $cn rankname)))
        ]
    ] [if $allowed [unspec $cn]]
] "rankgame" 

named_event_handler $onsetmaster [
    reference cn arg1
    reference set arg2
    
    mastermode $MM_LOCKED
    
    if (strcmp (player_priv $cn) "master") [
        setpriv $cn none
        msg (err "Players are not permitted to take master during a rank game.")
    ]
] "rankgame"

named_event_handler $onmapvote [
    reference cn arg1
    privmsg $cn (err "Voting is disabled during a rank game.")
    veto 1
] "rankgame"

endrankgame = [
    mastermode $MM_OPEN
    
    cancel_named_handler $onconnect rankgame
    cancel_named_handler $onrename rankgame
    cancel_named_handler $onsetmaster rankgame
    cancel_named_handler $onmapvote rankgame
    
    rankgame = 0
]
